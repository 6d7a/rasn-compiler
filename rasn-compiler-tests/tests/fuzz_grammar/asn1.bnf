<ModuleDefinition> ::= <ModuleIdentifier> <whitespace> <DEFINITIONS> <whitespace> <EncodingReferenceDefault> <whitespace> <TagDefault> <whitespace> <ExtensionDefault> <whitespace> "::=" <whitespace> <BEGIN> <whitespace> <ModuleBody> <whitespace> <EncodingControlSections> <whitespace> <END>
<ModuleIdentifier> ::= <modulereference> <whitespace> <DefinitiveIdentification>
<DefinitiveIdentification> ::= <DefinitiveOID> | <DefinitiveOIDandIRI> | <empty>
<DefinitiveOID> ::= "{" <whitespace> <DefinitiveObjIdComponentList> <whitespace> "}"
<DefinitiveOIDandIRI> ::= <DefinitiveOID> <whitespace> <IRIValue>
<DefinitiveObjIdComponentList> ::= <DefinitiveObjIdComponent> | <DefinitiveObjIdComponent> <whitespace> <DefinitiveObjIdComponentList>
<DefinitiveObjIdComponent> ::= <NameForm> | <DefinitiveNumberForm> | <DefinitiveNameAndNumberForm>
<DefinitiveNumberForm> ::= <number>
<DefinitiveNameAndNumberForm> ::= <identifier> <whitespace> "(" <whitespace> <DefinitiveNumberForm> <whitespace> ")"
<EncodingReferenceDefault> ::= <encodingreference> <whitespace> <INSTRUCTIONS> | <empty>
<TagDefault> ::= <EXPLICIT> <whitespace> <TAGS> | <IMPLICIT> <whitespace> <TAGS> | <AUTOMATIC> <whitespace> <TAGS> | <empty>
<ExtensionDefault> ::= <EXTENSIBILITY> <whitespace> <IMPLIED> | <empty>
<ModuleBody> ::= <Exports> <whitespace> <Imports> <whitespace> <AssignmentList> | <empty>
<Exports> ::= <EXPORTS> <whitespace> <SymbolsExported> <whitespace> ";" | <EXPORTS> <whitespace> <ALL> <whitespace> ";" | <empty>
<SymbolsExported> ::= <SymbolList> | <empty>
<Imports> ::= <IMPORTS> <whitespace> <SymbolsImported> <whitespace> ";" | <empty>
<SymbolsImported> ::= <SymbolsFromModuleList> | <empty>
<SymbolsFromModuleList> ::= <SymbolsFromModule> | <SymbolsFromModuleList> <whitespace> <SymbolsFromModule>
<SymbolsFromModule> ::= <SymbolList> <whitespace> <FROM> <whitespace> <GlobalModuleReference> <whitespace> <SelectionOption>
<SelectionOption> ::= <empty> | <WITH> <whitespace> "<SUCCESSORS>" | <WITH> <whitespace> "<DESCENDANTS>"
<GlobalModuleReference> ::= <modulereference> <whitespace> <AssignedIdentifier>
<AssignedIdentifier> ::= <ObjectIdentifierValue> | <DefinedValue> | <empty>
<SymbolList> ::= <Symbol> | <SymbolList> <whitespace> "," <whitespace> <Symbol>
<Symbol> ::= <Reference> | <ParameterizedReference>
<Reference> ::= <typereference> | <valuereference> | <objectclassreference> | <objectreference> | <objectsetreference>
<AssignmentList> ::= <Assignment> | <AssignmentList> <whitespace> <Assignment>
<Assignment> ::= <TypeAssignment> | <ValueAssignment> | <XMLValueAssignment> | <ValueSetTypeAssignment> | <ObjectClassAssignment> | <ObjectAssignment> | <ObjectSetAssignment> | <ParameterizedAssignment>
<DefinedType> ::= <ExternalTypeReference> | <typereference> | <ParameterizedType> | <ParameterizedValueSetType>
<DefinedValue> ::= <ExternalValueReference> | <valuereference> | <ParameterizedValue>
<NonParameterizedTypeName> ::= <ExternalTypeReference> | <typereference> | <xmlasn1typename>
<ExternalTypeReference> ::= <modulereference> <whitespace> "." <whitespace> <typereference>
<ExternalValueReference> ::= <modulereference> <whitespace> "." <whitespace> <valuereference>
<AbsoluteReference> ::= "@" <whitespace> <ModuleIdentifier> <whitespace> "." <whitespace> <ItemSpec>
<ItemSpec> ::= <typereference> | <ItemId> <whitespace> "." <whitespace> <ComponentId>
<ItemId> ::= <ItemSpec>
<ComponentId> ::= <identifier> | <number> | "*"
<TypeAssignment> ::= <typereference> <whitespace> "::=" <whitespace> <Type>
<ValueAssignment> ::= <valuereference> <whitespace> <Type> <whitespace> "::=" <whitespace> <Value>
<XMLValueAssignment> ::= <valuereference> <whitespace> "::=" <whitespace> <XMLTypedValue>
<XMLTypedValue> ::= "<" <NonParameterizedTypeName> <whitespace> ">" <whitespace> <XMLValue> <whitespace> "</" <NonParameterizedTypeName> <whitespace> ">" | "<" <NonParameterizedTypeName> <whitespace> "/>"
<ValueSetTypeAssignment> ::= <typereference> <whitespace> <Type> <whitespace> "::=" <whitespace> <ValueSet>
<ValueSet> ::= "{" <whitespace> <ElementSetSpecs> <whitespace> "}"
<Type> ::= <BuiltinType> | <ReferencedType> | <ConstrainedType>
<BuiltinType> ::= <BitStringType> | <BooleanType> | <CharacterStringType> | <ChoiceType> | <DateType> | <DateTimeType> | <DurationType> | <EmbeddedPDVType> | <EnumeratedType> | <ExternalType> | <InstanceOfType> | <IntegerType> | <IRIType> | <NullType> | <ObjectClassFieldType> | <ObjectIdentifierType> | <OctetStringType> | <RealType> | <RelativeIRIType> | <RelativeOIDType> | <SequenceType> | <SequenceOfType> | <SetType> | <SetOfType> | <PrefixedType> | <TimeType> | <TimeOfDayType>
<ReferencedType> ::= <DefinedType> | <UsefulType> | <SelectionType> | <TypeFromObject> | <ValueSetFromObjects>
<NamedType> ::= <identifier> <whitespace> <Type>
<Value> ::= <BuiltinValue> | <ReferencedValue> | <ObjectClassFieldValue>
<XMLValue> ::= <XMLBuiltinValue> | <XMLObjectClassFieldValue>
<BuiltinValue> ::= <BitStringValue> | <BooleanValue> | <CharacterStringValue> | <ChoiceValue> | <EmbeddedPDVValue> | <EnumeratedValue> | <ExternalValue> | <InstanceOfValue> | <IntegerValue> | <IRIValue> | <NullValue> | <ObjectIdentifierValue> | <OctetStringValue> | <RealValue> | <RelativeIRIValue> | <RelativeOIDValue> | <SequenceValue> | <SequenceOfValue> | <SetValue> | <SetOfValue> | <PrefixedValue> | <TimeValue>
<XMLBuiltinValue> ::= <XMLBitStringValue> | <XMLBooleanValue> | <XMLCharacterStringValue> | <XMLChoiceValue> | <XMLEmbeddedPDVValue> | <XMLEnumeratedValue> | <XMLExternalValue> | <XMLInstanceOfValue> | <XMLIntegerValue> | <XMLIRIValue> | <XMLNullValue> | <XMLObjectIdentifierValue> | <XMLOctetStringValue> | <XMLRealValue> | <XMLRelativeIRIValue> | <XMLRelativeOIDValue> | <XMLSequenceValue> | <XMLSequenceOfValue> | <XMLSetValue> | <XMLSetOfValue> | <XMLPrefixedValue> | <XMLTimeValue>
<ReferencedValue> ::= <DefinedValue> | <ValueFromObject>
<NamedValue> ::= <identifier> <whitespace> <Value>
<XMLNamedValue> ::= "<" <identifier> <whitespace> ">" <whitespace> <XMLValue> <whitespace> "</" <identifier> <whitespace> ">"
<BooleanType> ::=<BOOLEAN>
<BooleanValue> ::= <TRUE> | <FALSE>
<XMLBooleanValue> ::= <EmptyElementBoolean> | <TextBoolean>
<EmptyElementBoolean> ::= "<" "<true>" <whitespace> "/>" | "<" "<false>" <whitespace> "/>"
<TextBoolean> ::= <extended-true> | <extended-false>
<IntegerType> ::= <INTEGER> | <INTEGER> <whitespace> "{" <whitespace> <NamedNumberList> <whitespace> "}"
<NamedNumberList> ::= <NamedNumber> | <NamedNumberList> <whitespace> "," <whitespace> <NamedNumber>
<NamedNumber> ::= <identifier> <whitespace> "(" <whitespace> <SignedNumber> <whitespace> ")" | <identifier> <whitespace> "(" <whitespace> <DefinedValue> <whitespace> ")"
<SignedNumber> ::= <number> | "-" <whitespace> <number>
<IntegerValue> ::= <SignedNumber> | <identifier>
<XMLIntegerValue> ::= <XMLSignedNumber> | <EmptyElementInteger> | <TextInteger>
<XMLSignedNumber> ::= <number> | "-" <number>
<EmptyElementInteger> ::= "<" <identifier> <whitespace> "/>"
<TextInteger> ::= <identifier>
<EnumeratedType> ::= <ENUMERATED> <whitespace> "{" <whitespace> <Enumerations> <whitespace> "}"
<Enumerations> ::= <RootEnumeration> | <RootEnumeration> <whitespace> "," <whitespace> "..." <whitespace> <ExceptionSpec> | <RootEnumeration> <whitespace> "," <whitespace> "..." <whitespace> <ExceptionSpec> <whitespace> "," <whitespace> <AdditionalEnumeration>
<RootEnumeration> ::= <Enumeration>
<AdditionalEnumeration> ::= <Enumeration>
<Enumeration> ::= <EnumerationItem> | <EnumerationItem> <whitespace> "," <whitespace> <Enumeration>
<EnumerationItem> ::= <identifier> | <NamedNumber>
<EnumeratedValue> ::= <identifier>
<XMLEnumeratedValue> ::= <EmptyElementEnumerated> | <TextEnumerated>
<EmptyElementEnumerated> ::= "<" <identifier> <whitespace> "/>"
<TextEnumerated> ::= <identifier>
<RealType> ::= <REAL>
<RealValue> ::= <NumericRealValue> | <SpecialRealValue>
<NumericRealValue> ::= <realnumber> | "-" <whitespace> <realnumber> | <SequenceValue>
<SpecialRealValue> ::= <PLUS-INFINITY> | <MINUS-INFINITY> | <NOT-A-NUMBER>
<XMLRealValue> ::= <XMLNumericRealValue> | <XMLSpecialRealValue>
<XMLNumericRealValue> ::= <realnumber> | "-" <realnumber>
<XMLSpecialRealValue> ::= <EmptyElementReal> | <TextReal>
<EmptyElementReal> ::= "<" <PLUS-INFINITY> <whitespace> "/>" | "<" <MINUS-INFINITY> <whitespace> "/>" | "<" <NOT-A-NUMBER> <whitespace> "/>"
<TextReal> ::= "<INF>" | "-" "<INF>" | "<NaN>"
<BitStringType> ::= <BIT> <whitespace> <STRING> | <BIT> <whitespace> <STRING> <whitespace> "{" <whitespace> <NamedBitList> <whitespace> "}"
<NamedBitList> ::= <NamedBit> | <NamedBitList> <whitespace> "," <whitespace> <NamedBit>
<NamedBit> ::= <identifier> <whitespace> "(" <whitespace> <number> <whitespace> ")" | <identifier> <whitespace> "(" <whitespace> <DefinedValue> <whitespace> ")"
<BitStringValue> ::= <bstring> | <hstring> | "{" <whitespace> <IdentifierList> <whitespace> "}" | "{" <whitespace> "}" | <CONTAINING> <whitespace> <Value>
<IdentifierList> ::= <identifier> | <IdentifierList> <whitespace> "," <whitespace> <identifier>
<XMLBitStringValue> ::= <XMLTypedValue> | <xmlbstring> | <XMLIdentifierList> | <empty>
<XMLIdentifierList> ::= <EmptyElementList> | <TextList>
<EmptyElementList> ::= "<" <identifier> <whitespace> "/>" | <EmptyElementList> <whitespace> "<" <identifier> <whitespace> "/>"
<TextList> ::= <identifier> | <TextList> <whitespace> <identifier>
<OctetStringType> ::= <OCTET> <whitespace> <STRING>
<OctetStringValue> ::= <bstring> | <hstring> | <CONTAINING> <whitespace> <Value>
<XMLOctetStringValue> ::= <XMLTypedValue> | <xmlhstring>
<NullType> ::= <NULL>
<NullValue> ::= <NULL>
<XMLNullValue> ::= <empty>
<SequenceType> ::= <SEQUENCE> <whitespace> "{" <whitespace> "}" | <SEQUENCE> <whitespace> "{" <whitespace> <ExtensionAndException> <whitespace> <OptionalExtensionMarker> <whitespace> "}" | <SEQUENCE> <whitespace> "{" <whitespace> <ComponentTypeLists> <whitespace> "}"
<ExtensionAndException> ::= "..." | "..." <whitespace> <ExceptionSpec>
<OptionalExtensionMarker> ::= "," <whitespace> "..." | <empty>
<ComponentTypeLists> ::= <RootComponentTypeList> | <RootComponentTypeList> <whitespace> "," <whitespace> <ExtensionAndException> <whitespace> <ExtensionAdditions> <whitespace> <OptionalExtensionMarker> | <RootComponentTypeList> <whitespace> "," <whitespace> <ExtensionAndException> <whitespace> <ExtensionAdditions> <whitespace> <ExtensionEndMarker> <whitespace> "," <whitespace> <RootComponentTypeList> | <ExtensionAndException> <whitespace> <ExtensionAdditions> <whitespace> <ExtensionEndMarker> <whitespace> "," <whitespace> <RootComponentTypeList> | <ExtensionAndException> <whitespace> <ExtensionAdditions> <whitespace> <OptionalExtensionMarker>
<RootComponentTypeList> ::= <ComponentTypeList>
<ExtensionEndMarker> ::= "," <whitespace> "..."
<ExtensionAdditions> ::= "," <whitespace> <ExtensionAdditionList> | <empty>
<ExtensionAdditionList> ::= <ExtensionAddition> | <ExtensionAdditionList> <whitespace> "," <whitespace> <ExtensionAddition>
<ExtensionAddition> ::= <ComponentType> | <ExtensionAdditionGroup>
<ExtensionAdditionGroup> ::= "<[[>" <whitespace> <VersionNumber> <whitespace> <ComponentTypeList> <whitespace> "<]]>"
<VersionNumber> ::= <empty> | <number> <whitespace> ":"
<ComponentTypeList> ::= <ComponentType> | <ComponentTypeList> <whitespace> "," <whitespace> <ComponentType>
<ComponentType> ::= <NamedType> | <NamedType> <whitespace> <OPTIONAL> | <NamedType> <whitespace> <DEFAULT> <whitespace> <Value> | <COMPONENTS> <whitespace> <OF> <whitespace> <Type>
<SequenceValue> ::= "{" <whitespace> <ComponentValueList> <whitespace> "}" | "{" <whitespace> "}"
<ComponentValueList> ::= <NamedValue> | <ComponentValueList> <whitespace> "," <whitespace> <NamedValue>
<XMLSequenceValue> ::= <XMLComponentValueList> | <empty>
<XMLComponentValueList> ::= <XMLNamedValue> | <XMLComponentValueList> <whitespace> <XMLNamedValue>
<SequenceOfType> ::= <SEQUENCE> <whitespace> <OF> <whitespace> <Type> | <SEQUENCE> <whitespace> <OF> <whitespace> <NamedType>
<SequenceOfValue> ::= "{" <whitespace> <ValueList> <whitespace> "}" | "{" <whitespace> <NamedValueList> <whitespace> "}" | "{" <whitespace> "}"
<ValueList> ::= <Value> | <ValueList> <whitespace> "," <whitespace> <Value>
<NamedValueList> ::= <NamedValue> | <NamedValueList> <whitespace> "," <whitespace> <NamedValue>
<XMLSequenceOfValue> ::= <XMLValueList> | <XMLDelimitedItemList> | <empty>
<XMLValueList> ::= <XMLValueOrEmpty> | <XMLValueOrEmpty> <whitespace> <XMLValueList>
<XMLValueOrEmpty> ::= <XMLValue> | "<" <NonParameterizedTypeName> <whitespace> "/>"
<XMLDelimitedItemList> ::= <XMLDelimitedItem> | <XMLDelimitedItem> <whitespace> <XMLDelimitedItemList>
<XMLDelimitedItem> ::= "<" <NonParameterizedTypeName> <whitespace> ">" <whitespace> <XMLValue> <whitespace> "</" <NonParameterizedTypeName> <whitespace> ">" | "<" <identifier> <whitespace> ">" <whitespace> <XMLValue> <whitespace> "</" <identifier> <whitespace> ">"
<SetType> ::= <SET> <whitespace> "{" <whitespace> "}" | <SET> <whitespace> "{" <whitespace> <ExtensionAndException> <whitespace> <OptionalExtensionMarker> <whitespace> "}" | <SET> <whitespace> "{" <whitespace> <ComponentTypeLists> <whitespace> "}"
<SetValue> ::= "{" <whitespace> <ComponentValueList> <whitespace> "}" | "{" <whitespace> "}"
<XMLSetValue> ::= <XMLComponentValueList> | <empty>
<SetOfType> ::= <SET> <whitespace> <OF> <whitespace> <Type> | <SET> <whitespace> <OF> <whitespace> <NamedType>
<SetOfValue> ::= "{" <whitespace> <ValueList> <whitespace> "}" | "{" <whitespace> <NamedValueList> <whitespace> "}" | "{" <whitespace> "}"
<XMLSetOfValue> ::= <XMLValueList> | <XMLDelimitedItemList> | <empty>
<ChoiceType> ::= <CHOICE> <whitespace> "{" <whitespace> <AlternativeTypeLists> <whitespace> "}"
<AlternativeTypeLists> ::= <RootAlternativeTypeList> | <RootAlternativeTypeList> <whitespace> "," <whitespace> <ExtensionAndException> <whitespace> <ExtensionAdditionAlternatives> <whitespace> <OptionalExtensionMarker>
<RootAlternativeTypeList> ::= <AlternativeTypeList>
<ExtensionAdditionAlternatives> ::= "," <whitespace> <ExtensionAdditionAlternativesList> | <empty>
<ExtensionAdditionAlternativesList> ::= <ExtensionAdditionAlternative> | <ExtensionAdditionAlternativesList> <whitespace> "," <whitespace> <ExtensionAdditionAlternative>
<ExtensionAdditionAlternative> ::= <ExtensionAdditionAlternativesGroup> | <NamedType>
<ExtensionAdditionAlternativesGroup> ::= "<[[>" <whitespace> <VersionNumber> <whitespace> <AlternativeTypeList> <whitespace> "<]]>"
<AlternativeTypeList> ::= <NamedType> | <AlternativeTypeList> <whitespace> "," <whitespace> <NamedType>
<ChoiceValue> ::= <identifier> <whitespace> ":" <whitespace> <Value>
<XMLChoiceValue> ::= "<" <identifier> <whitespace> ">" <whitespace> <XMLValue> <whitespace> "</" <identifier> <whitespace> ">"
<SelectionType> ::= <identifier> <whitespace> "<" <whitespace> <Type>
<PrefixedType> ::= <TaggedType> | <EncodingPrefixedType> 
<PrefixedValue> ::= <Value>
<XMLPrefixedValue> ::= <XMLValue>
<TaggedType> ::= <Tag> <whitespace> <Type> | <Tag> <whitespace> <IMPLICIT> <whitespace> <Type> | <Tag> <whitespace> <EXPLICIT> <whitespace> <Type>
<Tag> ::= "<[>" <whitespace> <EncodingReference> <whitespace> <Class> <whitespace> <ClassNumber> <whitespace> "<]>"
<EncodingReference> ::= <encodingreference> <whitespace> ":" | <empty>
<ClassNumber> ::= <number> | <DefinedValue>
<Class> ::= <UNIVERSAL> | <APPLICATION> | <PRIVATE> | <empty>
<EncodingPrefixedType> ::= <EncodingPrefix> <whitespace> <Type>
<EncodingPrefix> ::= "<[>" <whitespace> <EncodingReference> <whitespace> <EncodingInstruction> <whitespace> "<]>" 
<ObjectIdentifierType> ::= <OBJECT> <whitespace> <IDENTIFIER>
<ObjectIdentifierValue> ::= "{" <whitespace> <ObjIdComponentsList> <whitespace> "}" | "{" <whitespace> <DefinedValue> <whitespace> <ObjIdComponentsList> <whitespace> "}"
<ObjIdComponentsList> ::= <ObjIdComponents> | <ObjIdComponents> <whitespace> <ObjIdComponentsList>
<ObjIdComponents> ::= <NameForm> | <NumberForm> | <NameAndNumberForm> | <DefinedValue>
<NameForm> ::= <identifier>
<NumberForm> ::= <number> | <DefinedValue>
<NameAndNumberForm> ::= <identifier> <whitespace> "(" <whitespace> <NumberForm> <whitespace> ")"
<XMLObjectIdentifierValue> ::= <XMLObjIdComponentList>
<XMLObjIdComponentList> ::= <XMLObjIdComponent> | <XMLObjIdComponent> "." <XMLObjIdComponentList>
<XMLObjIdComponent> ::= <NameForm> | <XMLNumberForm> | <XMLNameAndNumberForm>
<XMLNumberForm> ::= <number>
<XMLNameAndNumberForm> ::= <identifier> "(" <XMLNumberForm> ")"
<RelativeOIDType> ::= <RELATIVE-OID>
<RelativeOIDValue> ::= "{" <whitespace> <RelativeOIDComponentsList> <whitespace> "}"
<RelativeOIDComponentsList> ::= <RelativeOIDComponents> | <RelativeOIDComponents> <whitespace> <RelativeOIDComponentsList>
<RelativeOIDComponents> ::= <NumberForm> | <NameAndNumberForm> | <DefinedValue>
<XMLRelativeOIDValue> ::= <XMLRelativeOIDComponentList>
<XMLRelativeOIDComponentList> ::= <XMLRelativeOIDComponent> | <XMLRelativeOIDComponent> "." <XMLRelativeOIDComponentList>
<XMLRelativeOIDComponent> ::= <XMLNumberForm> | <XMLNameAndNumberForm>
<IRIType> ::= <OID-IRI>
<IRIValue> ::= """ <whitespace> <FirstArcIdentifier> <whitespace> <SubsequentArcIdentifier> <whitespace> """
<FirstArcIdentifier> ::= "/" <whitespace> <ArcIdentifier>
<SubsequentArcIdentifier> ::= "/" <whitespace> <ArcIdentifier> <whitespace> <SubsequentArcIdentifier> | <empty>
<ArcIdentifier> ::= <integerUnicodeLabel> | <non-integerUnicodeLabel>
<XMLIRIValue> ::= <FirstArcIdentifier> <whitespace> <SubsequentArcIdentifier>
<RelativeIRIType> ::= <RELATIVE-OID-IRI>
<RelativeIRIValue> ::= """ <whitespace> <FirstRelativeArcIdentifier> <whitespace> <SubsequentArcIdentifier> <whitespace> """
<FirstRelativeArcIdentifier> ::= <ArcIdentifier>
<XMLRelativeIRIValue> ::= <FirstRelativeArcIdentifier> <whitespace> <SubsequentArcIdentifier>
<EmbeddedPDVType> ::= <EMBEDDED> <whitespace> <PDV>
<EmbeddedPDVValue> ::= <SequenceValue>
<XMLEmbeddedPDVValue> ::= <XMLSequenceValue>
<ExternalType> ::= <EXTERNAL>
<ExternalValue> ::= <SequenceValue>
<XMLExternalValue> ::= <XMLSequenceValue>
<TimeType> ::= <TIME>
<TimeValue> ::= <tstring> 
<XMLTimeValue> ::= <xmltstring>
<DateType> ::= <DATE>
<TimeOfDayType> ::= <TIME-OF-DAY>
<DateTimeType> ::= <DATE-TIME>
<DurationType> ::= <DURATION>
<CharacterStringType> ::= <RestrictedCharacterStringType> | <UnrestrictedCharacterStringType>
<CharacterStringValue> ::= <RestrictedCharacterStringValue> | <UnrestrictedCharacterStringValue>
<XMLCharacterStringValue> ::= <XMLRestrictedCharacterStringValue> | <XMLUnrestrictedCharacterStringValue>
<RestrictedCharacterStringType> ::= <BMPString> | <GeneralString> | <GraphicString> | <IA5String> | <ISO646String> | <NumericString> | <PrintableString> | <TeletexString> | <T61String> | <UniversalString> | <UTF8String> | <VideotexString> | <VisibleString>
<RestrictedCharacterStringValue> ::= <cstring> | <CharacterStringList> | <Quadruple> | <Tuple>
<CharacterStringList> ::= "{" <whitespace> <CharSyms> <whitespace> "}"
<CharSyms> ::= <CharsDefn> | <CharSyms> <whitespace> "," <whitespace> <CharsDefn>
<CharsDefn> ::= <cstring> | <Quadruple> | <Tuple> | <DefinedValue>
<Quadruple> ::= "{" <whitespace> <Group> <whitespace> "," <whitespace> <Plane> <whitespace> "," <whitespace> <Row> <whitespace> "," <whitespace> <Cell> <whitespace> "}"
<Group> ::= <number>
<Plane> ::= <number>
<Row> ::= <number>
<Cell> ::= <number>
<Tuple> ::= "{" <whitespace> <TableColumn> <whitespace> "," <whitespace> <TableRow> <whitespace> "}"
<TableColumn> ::= <number>
<TableRow> ::= <number>
<XMLRestrictedCharacterStringValue> ::= <xmlcstring>
<UnrestrictedCharacterStringType> ::= <CHARACTER> <whitespace> <STRING>
<UnrestrictedCharacterStringValue> ::= <SequenceValue>
<XMLUnrestrictedCharacterStringValue> ::= <XMLSequenceValue>
<UsefulType> ::= <typereference>
<ConstrainedType> ::= <Type> <whitespace> <Constraint> | <TypeWithConstraint>
<TypeWithConstraint> ::= <SET> <whitespace> <Constraint> <whitespace> <OF> <whitespace> <Type> | <SET> <whitespace> <SizeConstraint> <whitespace> <OF> <whitespace> <Type> | <SEQUENCE> <whitespace> <Constraint> <whitespace> <OF> <whitespace> <Type> | <SEQUENCE> <whitespace> <SizeConstraint> <whitespace> <OF> <whitespace> <Type> | <SET> <whitespace> <Constraint> <whitespace> <OF> <whitespace> <NamedType> | <SET> <whitespace> <SizeConstraint> <whitespace> <OF> <whitespace> <NamedType> | <SEQUENCE> <whitespace> <Constraint> <whitespace> <OF> <whitespace> <NamedType> | <SEQUENCE> <whitespace> <SizeConstraint> <whitespace> <OF> <whitespace> <NamedType>
<Constraint> ::= "(" <whitespace> <ConstraintSpec> <whitespace> <ExceptionSpec> <whitespace> ")"
<ConstraintSpec> ::= <SubtypeConstraint> | <GeneralConstraint>
<SubtypeConstraint> ::= <ElementSetSpecs>
<ElementSetSpecs> ::= <RootElementSetSpec> | <RootElementSetSpec> <whitespace> "," <whitespace> "..." | <RootElementSetSpec> <whitespace> "," <whitespace> "..." <whitespace> "," <whitespace> <AdditionalElementSetSpec>
<RootElementSetSpec> ::= <ElementSetSpec>
<AdditionalElementSetSpec> ::= <ElementSetSpec>
<ElementSetSpec> ::= <Unions> | <ALL> <whitespace> <Exclusions>
<Unions> ::= <Intersections> | <UElems> <whitespace> <UnionMark> <whitespace> <Intersections>
<UElems> ::= <Unions>
<Intersections> ::= <IntersectionElements> | <IElems> <whitespace> <IntersectionMark> <whitespace> <IntersectionElements>
<IElems> ::= <Intersections>
<IntersectionElements> ::= <Elements> | <Elems> <whitespace> <Exclusions>
<Elems> ::= <Elements>
<Exclusions> ::= <EXCEPT> <whitespace> <Elements>
<UnionMark> ::= "|" | <UNION>
<IntersectionMark> ::= "<^>" | <INTERSECTION>
<Elements> ::= <SubtypeElements> | <ObjectSetElements> | "(" <whitespace> <ElementSetSpec> <whitespace> ")"
<SubtypeElements> ::= <SingleValue> | <ContainedSubtype> | <ValueRange> | <PermittedAlphabet> | <SizeConstraint> | <TypeConstraint> | <InnerTypeConstraints> | <PatternConstraint> | <PropertySettings> | <DurationRange> | <TimePointRange> | <RecurrenceRange>
<SingleValue> ::= <Value>
<ContainedSubtype> ::= <Includes> <whitespace> <Type>
<Includes> ::= <INCLUDES> | <empty>
<ValueRange> ::= <LowerEndpoint> <whitespace> ".." <whitespace> <UpperEndpoint>
<LowerEndpoint> ::= <LowerEndValue> | <LowerEndValue> <whitespace> "<"
<UpperEndpoint> ::= <UpperEndValue> | "<" <whitespace> <UpperEndValue>
<LowerEndValue> ::= <Value> | <MIN>
<UpperEndValue> ::= <Value> | <MAX>
<SizeConstraint> ::= <SIZE> <whitespace> <Constraint>
<TypeConstraint> ::= <Type>
<PermittedAlphabet> ::= <FROM> <whitespace> <Constraint>
<InnerTypeConstraints> ::= <WITH> <whitespace> <COMPONENT> <whitespace> <SingleTypeConstraint> | <WITH> <whitespace> <COMPONENTS> <whitespace> <MultipleTypeConstraints>
<SingleTypeConstraint> ::= <Constraint>
<MultipleTypeConstraints> ::= <FullSpecification> | <PartialSpecification>
<FullSpecification> ::= "{" <whitespace> <TypeConstraints> <whitespace> "}"
<PartialSpecification> ::= "{" <whitespace> "..." <whitespace> "," <whitespace> <TypeConstraints> <whitespace> "}"
<TypeConstraints> ::= <NamedConstraint> | <NamedConstraint> <whitespace> "," <whitespace> <TypeConstraints>
<NamedConstraint> ::= <identifier> <whitespace> <ComponentConstraint>
<ComponentConstraint> ::= <ValueConstraint> <whitespace> <PresenceConstraint>
<ValueConstraint> ::= <Constraint> | <empty>
<PresenceConstraint> ::= <PRESENT> | <ABSENT> | <OPTIONAL> | <empty>
<PatternConstraint> ::= <PATTERN> <whitespace> <Value>
<PropertySettings> ::= <SETTINGS> <whitespace> <simplestring>
<PropertySettingsList> ::= <PropertyAndSettingPair> | <PropertySettingsList> <whitespace> <PropertyAndSettingPair>
<PropertyAndSettingPair> ::= <PropertyName> <whitespace> "=" <whitespace> <SettingName>
<PropertyName> ::= <psname>
<SettingName> ::= <psname>
<DurationRange> ::= <ValueRange>
<TimePointRange> ::= <ValueRange>
<RecurrenceRange> ::= <ValueRange>
<ExceptionSpec> ::= "!" <whitespace> <ExceptionIdentification> | <empty>
<ExceptionIdentification> ::= <SignedNumber> | <DefinedValue> | <Type> <whitespace> ":" <whitespace> <Value>
<uppercase> ::= "A" | "B" | "C" | "D" | "E" | "F" | "G" | "H" | "I" | "J" | "K" | "L" | "M" | "N" | "O" | "P" | "Q" | "R" | "S" | "T" | "U" | "V" | "W" | "X" | "Y" | "Z"
<lowercase> ::=  "a" | "b" | "c" | "d" | "e" | "f" | "g" | "h" | "i" | "j" | "k" | "l" | "m" | "n" | "o" | "p" | "q" | "r" | "s" | "t" | "u" | "v" | "w" | "x" | "y" | "z"
<digit> ::= "0" | "1" | "2" | "3" | "4" | "5" | "6" | "7" | "8" | "9"
<non-null-digit> ::= "1" | "2" | "3" | "4" | "5" | "6" | "7" | "8" | "9"
<char-no-hyphen> ::= <uppercase> | <lowercase> | <digit>
<character> ::= <uppercase> | <lowercase> | <digit> | "-"
<text> ::= "" | <char-no-hyphen> <text> | "-" <char-no-hyphen> <text>
<upper-char-no-hyphen> ::= <uppercase> | <digit>
<uppercase-text> ::= "" | <upper-char-no-hyphen> <uppercase-text> | "-" <upper-char-no-hyphen> <uppercase-text>
<typereference> ::= <uppercase> <text>
<identifier> ::= <lowercase> <text>
<valuereference> ::= <lowercase> <text>
<modulereference> ::= <uppercase> <text>
<whitespace> ::= " "
<comment> ::= "-- This is a comment --"
<empty> ::= ""
<number> ::= <digit> | <digit> <number>
<realnumber> ::= <number> | <number> "." | <number> "." <number>
<bchar> ::= "0" | "1" | " "
<bstring> ::= "'" <xmlbstring> "'B"
<xmlbstring> ::= "" | <bchar> <xmlbstring>
<hchar> ::= <digit> | "A" | "B" | "C" | "D" | "E" | "F" | " "
<htext> ::= "" | <hchar> <htext>
<hstring> ::= "'" <htext> "'H"
<xmlhchar> ::= <hchar> | "a" | "b" | "c" | "d" | "e" | "f"
<xmlhstring> ::= "" | <xmlhchar> <xmlhstring>
<cstring> ::= "This is a string"
<xmlcstring> ::= "This is a string"
<simplestring> ::= "This is a string"
<tchar> ::= <digit> | "+" | "-" | ":" | "." | "," | "/" | "C" | "D" | "E" | "M" | "R" | "S" | "T" | "W" | "Y" | "Z"
<tstring> ::= <tchar> | <tchar> <tstring>
<xmltstring> ::= <tstring>
<psname> ::= <typereference>
<encodingreference> ::= <uppercase> <uppercase-text>
<integerUnicodeLabel> ::= "0" | <non-null-digit> | <non-null-digit> <digit>
<non-integerUnicodeLabel> ::= <identifier> | <typereference>
<extended-true> ::= "true" | "1"
<extended-false> ::= "false" | "0"
<xmlasn1typename> ::= "BIT_STRING" | "BOOLEAN" | "CHOICE" | "DATE" | "DATE_TIME" | "DURATION" | "SEQUENCE" | "ENUMERATED" | "SEQUENCE" | "SEQUENCE" | "INTEGER" | "OID_IRI" | "NULL" | "OBJECT_IDENTIFIER" | "OCTET_STRING" | "GeneralString" | "GraphicString" | "IA5String" | "ISO646String" | "NumericString" | "PrintableString" | "T61String" | "TeletexString" | "UniversalString" | "UTCTime" | "UTF8String" | "VideotexString" | "VisibleString" | "REAL" | "RELATIVE_OID_IRI" | "RELATIVE_OID" | "IA5String" | "SEQUENCE" | "SEQUENCE_OF" | "SET" | "SET_OF" | "TIME" | "TIME_OF_DAY" | "SEQUENCE"
<HYPHEN-MINUS> ::= "-"
<QUOTATION MARK> ::= '"'
<APOSTROPHE> ::= "'"
<SPACE> ::= " "
<ABSENT> ::= "ABSENT"
<ABSTRACT-SYNTAX> ::= "ABSTRACT-SYNTAX"
<ALL> ::= "ALL"
<APPLICATION> ::= "APPLICATION"
<AUTOMATIC> ::= "AUTOMATIC"
<BEGIN> ::= "BEGIN"
<BIT> ::= "BIT"
<BMPString> ::= "BMPString"
<BOOLEAN> ::= "BOOLEAN"
<BY> ::= "BY"
<CHARACTER> ::= "CHARACTER"
<CHOICE> ::= "CHOICE"
<CLASS> ::= "CLASS"
<COMPONENT> ::= "COMPONENT"
<COMPONENTS> ::= "COMPONENTS"
<CONSTRAINED> ::= "CONSTRAINED"
<CONTAINING> ::= "CONTAINING"
<DATE> ::= "DATE"
<DATE-TIME> ::= "DATE-TIME"
<DEFAULT> ::= "DEFAULT"
<DEFINITIONS> ::= "DEFINITIONS"
<DURATION> ::= "DURATION"
<EMBEDDED> ::= "EMBEDDED"
<ENCODED> ::= "ENCODED"
<ENCODING-CONTROL> ::= "ENCODING-CONTROL"
<END> ::= "END"
<ENUMERATED> ::= "ENUMERATED"
<EXCEPT> ::= "EXCEPT"
<EXPLICIT> ::= "EXPLICIT"
<EXPORTS> ::= "EXPORTS"
<EXTENSIBILITY> ::= "EXTENSIBILITY"
<EXTERNAL> ::= "EXTERNAL"
<FALSE> ::= "FALSE"
<FROM> ::= "FROM"
<GeneralizedTime> ::= "GeneralizedTime"
<GeneralString> ::= "GeneralString"
<GraphicString> ::= "GraphicString"
<IA5String> ::= "IA5String"
<IDENTIFIER> ::= "IDENTIFIER"
<IMPLICIT> ::= "IMPLICIT"
<IMPLIED> ::= "IMPLIED"
<IMPORTS> ::= "IMPORTS"
<INCLUDES> ::= "INCLUDES"
<INSTANCE> ::= "INSTANCE"
<INSTRUCTIONS> ::= "INSTRUCTIONS"
<INTEGER> ::= "INTEGER"
<INTERSECTION> ::= "INTERSECTION"
<ISO646String> ::= "ISO646String"
<MAX> ::= "MAX"
<MIN> ::= "MIN"
<MINUS-INFINITY> ::= "MINUS-INFINITY"
<NOT-A-NUMBER> ::= "NOT-A-NUMBER"
<NULL> ::= "NULL"
<NumericString> ::= "NumericString"
<OBJECT> ::= "OBJECT"
<ObjectDescriptor> ::= "ObjectDescriptor"
<OCTET> ::= "OCTET"
<OF> ::= "OF"
<OID-IRI> ::= "OID-IRI"
<OPTIONAL> ::= "OPTIONAL"
<PATTERN> ::= "PATTERN"
<PDV> ::= "PDV"
<PLUS-INFINITY> ::= "PLUS-INFINITY"
<PRESENT> ::= "PRESENT"
<PrintableString> ::= "PrintableString"
<PRIVATE> ::= "PRIVATE"
<REAL> ::= "REAL"
<RELATIVE-OID> ::= "RELATIVE-OID"
<RELATIVE-OID-IRI> ::= "RELATIVE-OID-IRI"
<SEQUENCE> ::= "SEQUENCE"
<SET> ::= "SET"
<SETTINGS> ::= "SETTINGS"
<SIZE> ::= "SIZE"
<STRING> ::= "STRING"
<SYNTAX> ::= "SYNTAX"
<T61String> ::= "T61String"
<TAGS> ::= "TAGS"
<TeletexString> ::= "TeletexString"
<TIME> ::= "TIME"
<TIME-OF-DAY> ::= "TIME-OF-DAY"
<TRUE> ::= "TRUE"
<TYPE-IDENTIFIER > ::= "TYPE-IDENTIFIER "
<UNION> ::= "UNION"
<UNIQUE> ::= "UNIQUE"
<UNIVERSAL> ::= "UNIVERSAL"
<UniversalString> ::= "UniversalString"
<UTCTime> ::= "UTCTime"
<UTF8String> ::= "UTF8String"
<VideotexString> ::= "VideotexString"
<VisibleString> ::= "VisibleString"
<WITH> ::= "WITH"